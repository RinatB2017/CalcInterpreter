Различные тела продукции записываются
либо через символ |, либо с новой строки.
Вместо некоторых терминалов (MUL, DIV, LP, ...) для наглядности
записаны соответствующие им лексемы, заключённые в '':
'*', '/', '(', ...

Скобки без обрамляющих '' используются для применения
квантификаторов (как в Perl, Java) к продукциям.

Тела продукций expr, term, ... записаны в правой рекурсии.

program:
	( EXIT | ';' | intsr )*

intsr:
	if_
	expr ';'
	void_func ';'

block:
	'{'	( ';' | instr )* '}'
	
if_:
	IF '(' expr ')' block
	IF '(' expr ')' block ELSE block
	
expr:
	term '+' expr
	term '-' expr
	term
	
term:
	power '*' power
	power '/' power
	power

power:
	factorial '^' power
	factorial

factorial:
	prim ( '!' )*
	
prim:
	NUMBER
	USER_DEFINED_NAME
	USER_DEFINED_NAME '=' expr
	'-' prim
	'(' expr ')'
	func

func:
	of_radian '(' expr ')'
	returns_radian '(' expr ')'
	LOG '(' expr ')'

of_radian:
	SIN
	COS

returns_radian:
	ARCSIN
	ARCCOS

void_func:
	print
	add
	del
	reset
	set
	help
	state
	
print:
	PRINT
	PRINT expr

add:
	USER_DEFINED_NAME
	USER_DEFINED_NAME '=' expr
	
del:
	DEL '*'
	DEL USER_DEFINED_NAME
	
reset:
	RESET '*'
	RESET setname
	RESET TABLE
	
set:
	SET setname '=' currTok
	
setname:
	ARGS_AUTO_END | AUTO_END | PRINT_TOKENS
	| PRECISION | ERRORS | STRICTED | AUTO_PRINT | GREEDY_FUNC
	
help:
	HELP
	
state:
	STATE