Различные тела продукции записываются
либо через символ | (ИЛИ), либо с новой строки.
Вместо некоторых терминалов (MUL, DIV, LP, ...) для наглядности
записаны соответствующие им лексемы, заключённые в '':
'*', '/', '(', ...
Начиная с символов '//' - и до конца строки - комметарий(как в C++, Java)

Скобки без обрамляющих '' используются для применения
квантификаторов (как в Perl, Java) к продукциям.

Тела продукций expr, term, ... записаны в правой рекурсии.

program:
	( ';' | intsr )* EXIT

<БНФ>
program:
	program` EXIT
	
program`:
	';' program`
	intsr program`
	e
</БНФ>
	
intsr:
	if_
	expr ';'
	void_func ';'

block:
	'{'	( ';' | instr )* '}'
	instr ';'

<БНФ>
block:
	'{'	block` '}'
	instr ';'

block`:
	';' block`
	instr block`
	e
</БНФ>
	
if_:
	IF '(' expr ')' block
	IF '(' expr ')' block ELSE block
	
expr:
	term '+' expr
	term '(+)' expr // сложение по модулю 2
	term '-' expr
	term
	
term:
	persent '*' term
	persent '/' term
	persent

persent:
	power '%'
	power
// 100+100+2^3% == 216

power:
	factorial '^' power
	factorial

factorial:
	prim ( '!' )*

<БНФ>
factorial:
	prim factorial`

factorial`:
	'!' factorial`
	e
</БНФ>
	
prim:
	NUMBER
	USER_DEFINED_NAME
	USER_DEFINED_NAME '=' expr
	USER_DEFINED_NAME '(' vector_dim ')' '=' vector
	vector
	vector postfix_func // постфиксная ф-я, например транспонирование
	'-' prim
	func
	
vector:
	'(' expr ( ',' expr )* ')'
	
<БНФ>
vector:
	'(' expr vector` ')'
	
vector`:
	',' expr vector`
	e
</БНФ>
	
vector_dim:
	USER_DEFINED_NAME ( ',' USER_DEFINED_NAME )*

<БНФ>
vector_dim:
	USER_DEFINED_NAME vector_dim`

vector_dim`:
	',' USER_DEFINED_NAME vector_dim`
	e
</БНФ>

func:
	of_radian '(' expr ')'
	returns_radian '(' expr ')'
	dimensionless '(' expr ')' // безразмерные ф-ии, например LOG

dimensionless:
	D // дифференциал
	INTEGRATE // интеграл
	LOG

of_radian:
	SIN
	COS

returns_radian:
	ARCSIN
	ARCCOS

void_func:
	print
	add
	del
	reset
	set
	help
	state
	
print:
	PRINT
	PRINT expr

add:
	USER_DEFINED_NAME
	USER_DEFINED_NAME '=' expr
	
del:
	DEL '*'
	DEL USER_DEFINED_NAME
	
reset:
	RESET '*'
	RESET setname
	RESET TABLE
	
set:
	SET setname '=' currTok
	
setname:
	ARGS_AUTO_END | AUTO_END | PRINT_TOKENS
	| PRECISION | ERRORS | STRICTED | AUTO_PRINT | GREEDY_FUNC
	
help:
	HELP
	
state:
	STATE